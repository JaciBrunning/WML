plugins {
  id "cpp"
  id "google-test-test-suite"
  id "edu.wpi.first.GradleRIO"
  id 'org.ysb33r.doxygen'
}

// Override these in your project gradle file
includeSrcInIncludeRoot = project.findProperty('includeSrcInIncludeRoot') ?: false
includeDesktopSupport = project.findProperty('includeDesktopSupport') ?: false
includeWMLRevSupport = project.findProperty('includeWMLRevSupport') ?: false

model {
  components {
    wml(NativeLibrarySpec) {
      targetPlatform wpi.platforms.roborio
      if (includeDesktopSupport) {
        targetPlatform wpi.platforms.desktop
      }

      sources.cpp {
        source {
          srcDir 'src/main/cpp'

          if (includeWMLRevSupport) {
            srcDir 'src/rev/cpp'
          }

          include '**/*.cpp', '**/*.cc'
        }
        exportedHeaders {
          srcDir 'src/main/include'
          if (includeSrcInIncludeRoot) {
            srcDir 'src/main/cpp'
          }

          if (includeWMLRevSupport) {
            srcDir 'src/rev/include'
            if (includeSrcInIncludeRoot) {
              srcDir 'src/rev/cpp'
            }
          }
        }
      }

      // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
      wpi.deps.wpilib(it)
      wpi.deps.vendor.cpp(it)
    }
  }

  testSuites {
    wmlTest(GoogleTestTestSuiteSpec) {
      testing $.components.wml

      sources.cpp {
        source {
          srcDir 'src/test/cpp'
          include '**/*.cpp'
        }
      }

      wpi.deps.wpilib(it)
      wpi.deps.googleTest(it)
      wpi.deps.vendor.cpp(it)
    }
  }
}

doxygen {
  executables {
    doxygen version : '1.8.13'
  }

  generate_html true
  javadoc_autobrief true
  source project.file('src/main/include')
  source project.file('src/rev/include')
}
